// epg

#define IMAGEFILE  "src/emu/epg_h.png"
purchase_sprites_with_icon(epg, 18, 0, 25ac)
MU_head_middle_sprites(11, epg_h, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/emu/epg_m1.png"
EMU_motor1_middle_sprites(10, epg_m1, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/emu/epg_m2.png"
EMU_motor1_middle_sprites(10, epg_m2, 32, 40)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, epg_b_sprites_start,
[  set_offset_to(-2),
   ((no_emu_ep_inR4()) * 2) +
   (prev_vehicle_randombits() & 0x01)])
{
  0: epg_m1_normal_sprites_left;
  1: epg_m2_normal_sprites_right;
  2: epg_m1_notpowered_sprites_left;
  3: epg_m2_notpowered_sprites_right;
     align_11_sprites;
}

switch (FEAT_TRAINS, SELF, epg_c_sprites_start,
[  set_offset_to(-4),
   ((no_emu_ep_inR4()) * 2) +
   (prev_vehicle_randombits() & 0x01)])
{
  0: epg_m2_sprites_left;
  1: epg_m1_sprites_right;
  2: epg_m2_notpowered_sprites_left;
  3: epg_m1_notpowered_sprites_right;
     align_11_sprites;
}

switch (FEAT_TRAINS, SELF, epg_never_train, position_in_articulated_veh)
{
  1: epg_m1_sprites_left;
  2: epg_m1_normal_sprites_left;
  3: epg_m2_sprites_right;
  4: epg_m2_normal_sprites_right;
  5: epg_m2_normal_sprites_left;
  6: epg_m1_normal_sprites_right;
     align_11_sprites;
}

switch (FEAT_TRAINS, SELF, epg_d_sprites_start,
   any_emu_ep())
{
   1: epg_h_sprites_right;
      epg_h_notpowered_sprites_right;
}

switch (FEAT_TRAINS, SELF, epg_sprites_start,
  position_in_articulated_veh)
{
    0: epg_h_sprites_left;
    2: epg_b_sprites_start;
    4: epg_c_sprites_start;
    6: epg_d_sprites_start;
  999: epg_never_train;
     dummy_sprites;
}

long_vehicle(epg)

switch (FEAT_TRAINS, SELF, epg_can_attach_wagon2,
  LOAD_TEMP(0) < 3)
{
  1: return CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, PARENT, epg_can_attach_wagon1,
  STORE_TEMP(emu_ep_count(), 0))
{
  epg_can_attach_wagon2;
}

switch (FEAT_TRAINS, SELF, epg_can_attach_wagon,
  any_emu_ep())
{
  1: epg_can_attach_wagon1;
     return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

switch (FEAT_TRAINS, SELF, epg_articulated_part,
  extra_callback_info1)
{
  1: return dummy3;
  2: return epg_part2;
  3: return dummy2;
  4: return epg_part2;
  5: return dummy2;
  6: return epg_part2;
  7: return dummy3;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, epg_c_create_effect1,
  electric_create_effect(4)) { return 1; }

switch (FEAT_TRAINS, PARENT, epg_c_create_effect,
  any_emu_ep())
{
  1: epg_c_create_effect1;
     return disable_create_effect();
}

switch (FEAT_TRAINS, SELF, epg_create_effect,
  position_in_articulated_veh)
{
  4: epg_c_create_effect;
     return disable_create_effect();
}

engine_power_MU3(epg, 2719, epr, eprii, epg)
engine_capacity_MU3(epg, 260, epr, eprii, epg)

RC_head_check3(epg, epr, eprii, epg)
switch (FEAT_TRAINS, SELF, epg_running_cost_factor,
[  STORE_TEMP(714, 0),  // Моторы
   STORE_TEMP(35, 1),   // Бригада
   STORE_TEMP(250, 2),  // Износ
   STORE_TEMP(0, 3),    // Сопровождение
   STORE_TEMP(175, 4),  // ТО
   STORE_TEMP(13, 5),   // Сертификация

   STORE_TEMP(160, 6),                 // Скорость
   STORE_TEMP(132, 7),                 // Тара
   STORE_TEMP(LOAD_TEMP(7) + 35, 8)])  // Максимальная масса
{ epg_check_running_cost_factor; }     // 439 * 2+187 * 2

name_in_group(epg, string(STR_NAME_IN_GROUP, string(STR_NAME_EP_SERIES), string(STR_NAME_EPG)),
                   string(STR_NAME_IN_GROUP, string(STR_NAME_EP_SERIES), string(STR_LONGNAME_EPG)))

hint_MU_with_subtype_sme2(epg,
     calc_loading(260 / (6 * WIDERST_D_DOOR)),
     STR_PURCHASE_HINT_CAP_0005_FAST,
     STR_PURCHASE_HINT_ENGINE_TYPE_EMU_SI_AC_WITH_SUBTYPE, STR_PURCHASE_HINT_FLIRT,
     STR_SECTIONS_MU_S1_3of4_WITH_ATTACH, STR_NAME_EPR, STR_NAME_EPRII,
     2011, 2012,
     string(STR_PURCHASE_HINT_FACTORY_BUSSNANG))

item (FEAT_TRAINS, epg, 225) {
  property {
    name: string(STR_NAME_EPG);
    vehicle_dates(2011, 2012, 30, 10, 8, 274)
    vehicle_emu(ac, 2719, 132, 200.0, 260, 6 * WIDERST_D_DOOR, )
    vehicle_group(group_ep)
  }
  graphics {
        additional_text: epg_additional_text;
       articulated_part: epg_articulated_part;
       can_attach_wagon: epg_can_attach_wagon;
       cargo_age_period: return CAP_0005_FAST;
         cargo_capacity: epg_cargo_capacity;
         colour_mapping: any_cc_colour;
            cost_factor: return 274;
          create_effect: epg_create_effect;
                default: epg_sprites;
                   name: epg_name;
                  power: epg_power;
               purchase: epg_purchase_sprites;
         purchase_speed: return 160;
    running_cost_factor: epg_running_cost_factor;
                  speed: return 160;
  }
}

long_name_template(epg, STR_LONGNAME_EPG)
allow_acemu(epg)

item (FEAT_TRAINS, epg_part2, 455) {
  property {
    vehicle_no_dates(2011, 2012, 30, 10, 8, 0)
    vehicle_emu_empty(ac, )
  }
  graphics {
    colour_mapping: any_cc_colour;
     create_effect: epg_create_effect;
           default: epg_sprites;
  }
}
