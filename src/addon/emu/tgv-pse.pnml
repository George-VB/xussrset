// tgv_pse

#define IMAGEFILE  "src/addon/emu/tgv-pse_01.png"
purchase_sprites_with_icon(tgv_pse, 18, 0, 25ac_15dc)
MU_head_2d_middle_sp_sprites(12, tgv_pse_h_v1, 32, 40)
MU_head_2d_middle_sp_sprites(12, tgv_pse_h_v2, 32, 576)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-pse_01-part2.png"
MU_head_2d_middle_sp_sprites(12, tgv_pse_h_v3, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-pse_02.png"
MU_car_sprites(12, tgv_pse_02_v1, 32, 40)
MU_car_sprites(12, tgv_pse_02_v2, 32, 308)
MU_car_sprites(12, tgv_pse_02_v3, 32, 576)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-pse_03.png"
MU_car_sprites(11, tgv_pse_03_v1, 32, 40)
MU_car_sprites(11, tgv_pse_03_v2, 32, 308)
MU_car_sprites(11, tgv_pse_03_v3, 32, 576)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-pse_05.png"
MU_car_nd_sprites(11, tgv_pse_05_v1, 32, 40)
MU_car_nd_sprites(11, tgv_pse_05_v2, 32, 308)
MU_car_nd_sprites(11, tgv_pse_05_v3, 32, 576)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-pse_06.png"
MU_car_sprites(11, tgv_pse_06_v1, 32, 40)
MU_car_sprites(11, tgv_pse_06_v2, 32, 308)
MU_car_sprites(11, tgv_pse_06_v3, 32, 576)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-pse_09.png"
MU_car_sprites(12, tgv_pse_09_v1, 32, 40)
MU_car_sprites(12, tgv_pse_09_v2, 32, 308)
MU_car_sprites(12, tgv_pse_09_v3, 32, 576)
#undef IMAGEFILE

#define tgv_pse_graphics_template(name, suffix)           \
switch (FEAT_TRAINS, SELF, name##suffix##_sprites_left,   \
[ set_offset_to(-position_in_articulated_veh),            \
  prev_vehicle_cargo_subtype()])                          \
{                  0: name##_v1##suffix##_sprites_left;   \
  LV_SNCF_ATLANTIQUE: name##_v2##suffix##_sprites_left;   \
   LV_SNCF_CARMILLON: name##_v3##suffix##_sprites_left;   \
     align_12_sprites; }                                  \
switch (FEAT_TRAINS, SELF, name##suffix##_sprites_right,  \
[ set_offset_to(-position_in_articulated_veh),            \
  prev_vehicle_cargo_subtype()])                          \
{                  0: name##_v1##suffix##_sprites_right;  \
  LV_SNCF_ATLANTIQUE: name##_v2##suffix##_sprites_right;  \
   LV_SNCF_CARMILLON: name##_v3##suffix##_sprites_right;  \
     align_12_sprites; }                                  \

tgv_pse_graphics_template(tgv_pse_h, )
tgv_pse_graphics_template(tgv_pse_h, _middle)
tgv_pse_graphics_template(tgv_pse_h, _notpowered)
tgv_pse_graphics_template(tgv_pse_02, )
tgv_pse_graphics_template(tgv_pse_02, _notpowered)
tgv_pse_graphics_template(tgv_pse_03, )
tgv_pse_graphics_template(tgv_pse_03, _notpowered)
tgv_pse_graphics_template(tgv_pse_05, )
tgv_pse_graphics_template(tgv_pse_06, )
tgv_pse_graphics_template(tgv_pse_06, _notpowered)
tgv_pse_graphics_template(tgv_pse_09, )
tgv_pse_graphics_template(tgv_pse_09, _notpowered)

// choose graphics block

switch (FEAT_TRAINS, SELF, tgv_pse_h_sprites_left_check,
  position_in_consist == 0)
{
  1: tgv_pse_h_sprites_left;
     tgv_pse_h_middle_sprites_left;
}

switch (FEAT_TRAINS, SELF, tgv_pse_h_sprites_right_check,
  position_in_consist_from_end == 1)
{
  1: tgv_pse_h_sprites_right;
     tgv_pse_h_middle_sprites_right;
}

switch (FEAT_TRAINS, SELF, tgv_pse_sprites_start,
[  set_offset_to(-position_in_articulated_veh),
   (no_tgv_inR4() * 200) +
   (prev_vehicle_randombits() & 0x01) * 100 +
   position_in_articulated_veh])
{
   0: tgv_pse_h_sprites_left_check;
   2: tgv_pse_02_sprites_left;
   4: tgv_pse_03_sprites_left;
   6: tgv_pse_03_sprites_left;
   8: tgv_pse_05_sprites_left;
  10: tgv_pse_06_sprites_right;
  12: tgv_pse_06_sprites_right;
  14: tgv_pse_06_sprites_right;
  16: tgv_pse_09_sprites_right;
  18: tgv_pse_h_sprites_right_check;

 100: tgv_pse_h_sprites_left_check;
 102: tgv_pse_09_sprites_left;
 104: tgv_pse_06_sprites_left;
 106: tgv_pse_06_sprites_left;
 108: tgv_pse_06_sprites_left;
 110: tgv_pse_05_sprites_right;
 112: tgv_pse_03_sprites_right;
 114: tgv_pse_03_sprites_right;
 116: tgv_pse_02_sprites_right;
 118: tgv_pse_h_sprites_right_check;

 200: tgv_pse_h_notpowered_sprites_left;
 202: tgv_pse_02_notpowered_sprites_left;
 204: tgv_pse_03_notpowered_sprites_left;
 206: tgv_pse_03_notpowered_sprites_left;
 208: tgv_pse_05_sprites_left;
 210: tgv_pse_06_notpowered_sprites_right;
 212: tgv_pse_06_notpowered_sprites_right;
 214: tgv_pse_06_notpowered_sprites_right;
 216: tgv_pse_09_notpowered_sprites_right;
 218: tgv_pse_h_notpowered_sprites_right;

 300: tgv_pse_h_notpowered_sprites_left;
 302: tgv_pse_09_notpowered_sprites_left;
 304: tgv_pse_06_notpowered_sprites_left;
 306: tgv_pse_06_notpowered_sprites_left;
 308: tgv_pse_06_notpowered_sprites_left;
 310: tgv_pse_05_sprites_right;
 312: tgv_pse_03_notpowered_sprites_right;
 314: tgv_pse_03_notpowered_sprites_right;
 316: tgv_pse_02_notpowered_sprites_right;
 318: tgv_pse_h_notpowered_sprites_right;

      align_11_sprites;
}

long_vehicle(tgv_pse)

switch (FEAT_TRAINS, SELF, tgv_pse_articulated_part,
  extra_callback_info1)
{
  1: return dummy4;
  2: return tgv_pse_part2;
  3: return dummy4;
  4: return tgv_pse_part3;
  5: return dummy3;
  6: return tgv_pse_part3;
  7: return dummy3;
  8: return tgv_pse_part3;
  9: return dummy3;
 10: return tgv_pse_part3;
 11: return dummy3;
 12: return tgv_pse_part3;
 13: return dummy3;
 14: return tgv_pse_part3;
 15: return dummy3;
 16: return tgv_pse_part3;
 17: return dummy4;
 18: return tgv_pse_part3;
 19: return dummy4;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, tgv_pse_create_effect_01,
  electric_create_effect(3)) { return 1; }

switch (FEAT_TRAINS, SELF, tgv_pse_create_effect_10,
  electric_create_effect(-2)) { return 1; }

switch (FEAT_TRAINS, SELF, tgv_pse_create_effect0,
  position_in_articulated_veh)
{
   0: tgv_pse_create_effect_01;
  18: tgv_pse_create_effect_10;
      return disable_create_effect();
}

switch (FEAT_TRAINS, PARENT, tgv_pse_create_effect,
  any_tgv())
{
  1: tgv_pse_create_effect0;
     return disable_create_effect();
}

switch (FEAT_TRAINS, PARENT, tgv_pse_can_attach_wagon1,
  tgvs_count() < 2)
{
  1: return CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, SELF, tgv_pse_can_attach_wagon,
  any_tgv())
{
  1: tgv_pse_can_attach_wagon1;
     return string(STR_CAN_ATTACH_TGV);
}

switch (FEAT_TRAINS, SELF, tgv_pse_cargo_subtype_text,
  cargo_subtype)
{
   LV_SNCF_CARMILLON: return string(STR_REFIT_LV_SNCF_CARMILLON);
  LV_SNCF_ATLANTIQUE: return string(STR_REFIT_LV_SNCF_ATLANTIQUE);
                      return string(STR_REFIT_FACTORY);
}

RC_head_check_tgv(tgv_pse)
switch (FEAT_TRAINS, SELF, tgv_pse_running_cost_factor_AC,
[  STORE_TEMP(1561, 0),  // Моторы
   STORE_TEMP(219, 1),   // Бригада
   STORE_TEMP(548, 2),   // Износ
   STORE_TEMP(0, 3),     // Сопровождение
   STORE_TEMP(383, 4),   // ТО
   STORE_TEMP(27, 5),    // Сертификация

   STORE_TEMP(270, 6),                                 // Скорость
   STORE_TEMP(385, 7),                                 // Тара
   STORE_TEMP(LOAD_TEMP(7) + 260 / 16 + 108 / 8, 8)])  // Максимальная масса
{ tgv_pse_check_running_cost_factor; }
switch (FEAT_TRAINS, SELF, tgv_pse_running_cost_factor_DC,
[  STORE_TEMP(1561, 0),  // Моторы
   STORE_TEMP(219, 1),   // Бригада
   STORE_TEMP(548, 2),   // Износ
   STORE_TEMP(0, 3),     // Сопровождение
   STORE_TEMP(383, 4),   // ТО
   STORE_TEMP(27, 5),    // Сертификация

   STORE_TEMP(250, 6),                                 // Скорость
   STORE_TEMP(385, 7),                                 // Тара
   STORE_TEMP(LOAD_TEMP(7) + 260 / 16 + 108 / 8, 8)])  // Максимальная масса
{ tgv_pse_check_running_cost_factor; }

switch (FEAT_TRAINS, SELF, tgv_pse_running_cost_factor, is_ER25AC() || is_ER15AC())
{ 1: tgv_pse_running_cost_factor_AC;
     tgv_pse_running_cost_factor_DC; }

// TODO сделать модернизацию скорости до 300 и разновидность трёхсистемную 16 кВ с 2800 квт

engine_capacity_MU_tgv(tgv_pse, 260)
engine_power_MU_tgv(tgv_pse_15DC, kw2hp(3100))
engine_power_MU_tgv(tgv_pse_AC, kw2hp(6450))
engine_power_change_AC_15DC(tgv_pse)
engine_speed(tgv_pse_DC, 250)
engine_speed(tgv_pse_AC, 270)
engine_speed_change_AC_DC(tgv_pse)
hint_MU_comment_chande_speedpower_on_dc(tgv_pse,
     calc_loading(max(260 / (4 * WAGON_DOOR), 108 / (3 * WAGON_DOOR))),
     STR_PURCHASE_HINT_CAP_001_NORMAL,
     STR_PURCHASE_HINT_ENGINE_TYPE_EMU_T_AC15DC,
     STR_SECTIONS_MU_S1_2of10,
     1978, 1988,
     string(STR_PURCHASE_HINT_FACTORY_ALSTOM_TRANS),
     string(STR_PURCHASE_HINT_TGV),
     250, kw2hp_in_cb(3100))

item (FEAT_TRAINS, tgv_pse, 131) {
  property {
    name: string(STR_NAME_TGV_PSE);
    vehicle_dates(1978, 1988, 30, 10, 8, 1500)
    vehicle_emu(ac15dc, kw2hp(6450), 385, 220.0, 260, 4 * WAGON_DOOR, _highspeed)
  }
  graphics {
        additional_text: tgv_pse_additional_text;
       articulated_part: tgv_pse_articulated_part;
       can_attach_wagon: tgv_pse_can_attach_wagon;
       cargo_age_period: return CAP_001_NORMAL;
         cargo_capacity: tgv_pse_cargo_capacity;
     cargo_subtype_text: tgv_pse_cargo_subtype_text;
         colour_mapping: any_cc_colour;
            cost_factor: return 1500;
          create_effect: tgv_pse_create_effect;
                default: tgv_pse_sprites;
                  power: tgv_pse_power;
               purchase: tgv_pse_purchase_sprites;
         purchase_power: return get_power_in_cb(kw2hp(6450));
         purchase_speed: return 270;
    running_cost_factor: tgv_pse_running_cost_factor;
                  speed: tgv_pse_speed;
  }
}

long_name_template(tgv_pse, STR_LONGNAME_TGV_PSE)

engine_capacity_MU_tgv(tgv_pse_part2, 108)

item (FEAT_TRAINS, tgv_pse_part2, 132) {
  property {
    vehicle_no_dates(1978, 1988, 30, 10, 8, 0)
    vehicle_emu_c(ac15dc, 0, 108, 3 * WAGON_DOOR, _highspeed)
    vehicle_tourists()
    default_cargo_type: TOUR;
  }
  graphics {
    cargo_age_period: return CAP_001_NORMAL;
      cargo_capacity: tgv_pse_part2_cargo_capacity;
      colour_mapping: any_cc_colour;
       create_effect: tgv_pse_create_effect;
             default: tgv_pse_sprites;
          refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}

item (FEAT_TRAINS, tgv_pse_part3, 133) {
  property {
    vehicle_no_dates(1978, 1988, 30, 10, 8, 0)
    vehicle_emu_empty(ac15dc, _highspeed)
  }
  graphics {
    colour_mapping: any_cc_colour;
     create_effect: tgv_pse_create_effect;
           default: tgv_pse_sprites;
  }
}
