// talgo350

#define IMAGEFILE  "src/addon/emu/talgo350_01.png"
purchase_sprites_with_icon(talgo350, 18, 0, 25ac)
MU_head_sprites(11, talgo350_h, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/talgo350_02.png"
MU_car_nd_sprites(8, talgo350_02, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/talgo350_03.png"
MU_car_sprites(8, talgo350_03, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/talgo350_07.png"
MU_car_sprites(8, talgo350_07, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/talgo350_08.png"
MU_car_nd_sprites(8, talgo350_08, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/talgo350_09.png"
MU_car_sprites(8, talgo350_09, 32, 40)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/talgo350_13.png"
MU_car_sprites(8, talgo350_13, 32, 40)
#undef IMAGEFILE

switch (FEAT_TRAINS, SELF, talgo350_h_sprites_left_check,
  position_in_consist == 0)
{
  1: talgo350_h_sprites_left;
     talgo350_h_notpowered_sprites_left;
}

switch (FEAT_TRAINS, SELF, talgo350_h_sprites_right_check,
  position_in_consist_from_end == 1)
{
  1: talgo350_h_sprites_right;
     talgo350_h_notpowered_sprites_right;
}

switch (FEAT_TRAINS, SELF, talgo350_sprites_start,
[  set_offset_to(-position_in_articulated_veh),
   ((LOAD_TEMP(4) != talgo350) * 200) +
   (prev_vehicle_randombits() & 0x01) * 100 +
   position_in_articulated_veh])
{
   0: talgo350_h_sprites_left_check;
   2: talgo350_02_sprites_left;
   3: talgo350_03_sprites_left;
   4: talgo350_03_sprites_left;
   5: talgo350_03_sprites_left;
   6: talgo350_03_sprites_left;
   7: talgo350_07_sprites_left;
   8: talgo350_08_sprites_left;
   9: talgo350_09_sprites_left;
  10: talgo350_09_sprites_left;
  11: talgo350_09_sprites_left;
  12: talgo350_09_sprites_left;
  13: talgo350_13_sprites_left;
  14: talgo350_h_sprites_right_check;

 100: talgo350_h_sprites_left_check;
 102: talgo350_13_sprites_right;
 103: talgo350_09_sprites_right;
 104: talgo350_09_sprites_right;
 105: talgo350_09_sprites_right;
 106: talgo350_09_sprites_right;
 107: talgo350_08_sprites_right;
 108: talgo350_07_sprites_right;
 109: talgo350_03_sprites_right;
 110: talgo350_03_sprites_right;
 111: talgo350_03_sprites_right;
 112: talgo350_03_sprites_right;
 113: talgo350_02_sprites_right;
 114: talgo350_h_sprites_right_check;

 200: talgo350_h_notpowered_sprites_left;
 202: talgo350_02_sprites_left;
 203: talgo350_03_notpowered_sprites_left;
 204: talgo350_03_notpowered_sprites_left;
 205: talgo350_03_notpowered_sprites_left;
 206: talgo350_03_notpowered_sprites_left;
 207: talgo350_07_notpowered_sprites_left;
 208: talgo350_08_sprites_left;
 209: talgo350_09_notpowered_sprites_left;
 210: talgo350_09_notpowered_sprites_left;
 211: talgo350_09_notpowered_sprites_left;
 212: talgo350_09_notpowered_sprites_left;
 213: talgo350_13_notpowered_sprites_left;
 214: talgo350_h_notpowered_sprites_right;

 300: talgo350_h_notpowered_sprites_left;
 302: talgo350_13_notpowered_sprites_right;
 303: talgo350_09_notpowered_sprites_right;
 304: talgo350_09_notpowered_sprites_right;
 305: talgo350_09_notpowered_sprites_right;
 306: talgo350_09_notpowered_sprites_right;
 307: talgo350_08_sprites_right;
 308: talgo350_07_notpowered_sprites_right;
 309: talgo350_03_notpowered_sprites_right;
 310: talgo350_03_notpowered_sprites_right;
 311: talgo350_03_notpowered_sprites_right;
 312: talgo350_03_notpowered_sprites_right;
 313: talgo350_02_sprites_right;
 314: talgo350_h_notpowered_sprites_right;

      align_11_sprites;
}

long_vehicle(talgo350)

switch (FEAT_TRAINS, SELF, talgo350_articulated_part,
  extra_callback_info1)
{
  1: return dummy3;
  2: return talgo350_part2;
  3: return talgo350_part3;
  4: return talgo350_part3;
  5: return talgo350_part3;
  6: return talgo350_part3;
  7: return talgo350_part3;
  8: return talgo350_part3;
  9: return talgo350_part3;
 10: return talgo350_part3;
 11: return talgo350_part3;
 12: return talgo350_part3;
 13: return talgo350_part3;
 14: return talgo350_part3;
 15: return dummy3;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, talgo350_create_effect3,
  electric_create_effect(-3)) { return 1; }

switch (FEAT_TRAINS, SELF, talgo350_create_effect0,
  position_in_articulated_veh)
{
  14: talgo350_create_effect3;
      return disable_create_effect();
}

switch (FEAT_TRAINS, PARENT, talgo350_create_effect,
  vehicle_type_id == talgo350)
{
  1: talgo350_create_effect0;
     return disable_create_effect();
}

switch (FEAT_TRAINS, SELF, talgo350_can_attach_wagon,
  vehicle_type_id == talgo350)
{
  1: return string(STR_CAN_ATTACH_NO_MORE);
     return string(STR_CAN_ATTACH_SAME_MU_ONLY);
}

RC_head_check(talgo350, talgo350)
switch (FEAT_TRAINS, SELF, talgo350_running_cost_factor,
[  STORE_TEMP(1561, 0),  // Моторы
   STORE_TEMP(219, 1),   // Бригада
   STORE_TEMP(548, 2),   // Износ
   STORE_TEMP(0, 3),     // Сопровождение
   STORE_TEMP(383, 4),   // ТО
   STORE_TEMP(27, 5),    // Сертификация

   STORE_TEMP(220, 6),                                // Скорость
   STORE_TEMP(322, 7),                                // Тара
   STORE_TEMP(LOAD_TEMP(7) + 318 / 16 + 48 / 8, 8)])  // Максимальная масса
{ talgo350_check_running_cost_factor; }               // 1921+817

engine_capacity_MU(talgo350, 318, talgo350)
engine_power_MU(talgo350, 10876, talgo350)
engine_speed(talgo350, 300)
hint_MU_with_subtype_noto(talgo350,
     calc_loading(max(318 / (10 * WAGON_DOOR), 48 / (WAGON_DOOR))),
     STR_PURCHASE_HINT_CAP_001_NORMAL,
     STR_PURCHASE_HINT_ENGINE_TYPE_EMU_T_AC_WITH_SUBTYPE, STR_PURCHASE_HINT_PENDOLINO,
     STR_SECTIONS_MU_S1of14,
     2005,
     string(STR_PURCHASE_HINT_FACTORY_PATENTES_TALGO))

item (FEAT_TRAINS, talgo350, 181) {
  property {
    name: string(STR_NAME_TALGO350);
    vehicle_dates(2005, 2050, 30, 10, 8, 1200)
    vehicle_emu(ac, 10876, 322, 200.0, 318, 10 * WAGON_DOOR, _highspeed)
  }
  graphics {
        additional_text: talgo350_additional_text;
       articulated_part: talgo350_articulated_part;
       can_attach_wagon: talgo350_can_attach_wagon;
       cargo_age_period: return CAP_001_NORMAL;
         cargo_capacity: talgo350_cargo_capacity;
         colour_mapping: any_cc_colour;
            cost_factor: return 1200;
          create_effect: talgo350_create_effect;
                default: talgo350_sprites;
                  power: talgo350_power;
               purchase: talgo350_purchase_sprites;
         purchase_speed: return 300;
    running_cost_factor: talgo350_running_cost_factor;
                  speed: talgo350_speed;
  }
}

long_name_template(talgo350, STR_LONGNAME_TALGO350)

engine_capacity_MU(talgo350_part2, 48, talgo350)

item (FEAT_TRAINS, talgo350_part2, 182) {
  property {
    vehicle_no_dates(2005, 2050, 30, 10, 8, 0)
    vehicle_emu_c(ac, 0, 48, 1 * WAGON_DOOR, _highspeed)
    vehicle_tourists()
    default_cargo_type: TOUR;
  }
  graphics {
    cargo_age_period: return CAP_001_NORMAL;
      cargo_capacity: talgo350_part2_cargo_capacity;
      colour_mapping: any_cc_colour;
       create_effect: talgo350_create_effect;
             default: talgo350_sprites;
          refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}

item (FEAT_TRAINS, talgo350_part3, 143) {
  property {
    vehicle_no_dates(2005, 2050, 30, 10, 8, 0)
    vehicle_emu_empty(ac, _highspeed)
  }
  graphics {
    colour_mapping: any_cc_colour;
     create_effect: talgo350_create_effect;
           default: talgo350_sprites;
  }
}
