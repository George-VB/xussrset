// tgv_rd uses tgv_duplex

#define IMAGEFILE  "src/addon/emu/tgv-rd_01.png"
purchase_sprites_with_icon2(tgv_rd, 18, 0, 25ac_ddc, none)
MU_head_2d_middle_sp_sprites(12, tgv_rd_h_v1, 32, 40)
MU_head_2d_middle_sp_sprites(12, tgv_rd_h_v2, 32, 576)
#undef IMAGEFILE

#define tgv_rd_graphics_template(name, name2, suffix)      \
switch (FEAT_TRAINS, SELF, name##suffix##_sprites_left,    \
[ set_offset_to(-position_in_articulated_veh),             \
  prev_vehicle_cargo_subtype()])                           \
{                  0: name2##_v1##suffix##_sprites_left;   \
  LV_SNCF_ATLANTIQUE: name2##_v1##suffix##_sprites_left;   \
   LV_SNCF_CARMILLON: name2##_v2##suffix##_sprites_left;   \
     align_12_sprites; }                                   \
switch (FEAT_TRAINS, SELF, name##suffix##_sprites_right,   \
[ set_offset_to(-position_in_articulated_veh),             \
  prev_vehicle_cargo_subtype()])                           \
{                  0: name2##_v1##suffix##_sprites_right;  \
  LV_SNCF_ATLANTIQUE: name2##_v1##suffix##_sprites_right;  \
   LV_SNCF_CARMILLON: name2##_v2##suffix##_sprites_right;  \
     align_12_sprites; }                                   \

#define tgv_rd_graphics_template2(name, suffix)           \
switch (FEAT_TRAINS, SELF, name##suffix##_sprites_left,   \
[ set_offset_to(-position_in_articulated_veh),            \
  prev_vehicle_cargo_subtype()])                          \
{                  0: name##_v1##suffix##_sprites_left;   \
  LV_SNCF_ATLANTIQUE: name##_v1##suffix##_sprites_left;   \
   LV_SNCF_CARMILLON: name##_v2##suffix##_sprites_left;   \
     align_12_sprites; }                                  \
switch (FEAT_TRAINS, SELF, name##suffix##_sprites_right,  \
[ set_offset_to(-position_in_articulated_veh),            \
  prev_vehicle_cargo_subtype()])                          \
{                  0: name##_v1##suffix##_sprites_right;  \
  LV_SNCF_ATLANTIQUE: name##_v1##suffix##_sprites_right;  \
   LV_SNCF_CARMILLON: name##_v2##suffix##_sprites_right;  \
     align_12_sprites; }                                  \

tgv_rd_graphics_template2(tgv_rd_h, )
tgv_rd_graphics_template2(tgv_rd_h, _middle)
tgv_rd_graphics_template2(tgv_rd_h, _notpowered)
tgv_rd_graphics_template(tgv_rd_02, tgv_duplex_02, )
tgv_rd_graphics_template(tgv_rd_02, tgv_duplex_02, _notpowered)
tgv_rd_graphics_template(tgv_rd_03, tgv_duplex_03, )
tgv_rd_graphics_template(tgv_rd_03, tgv_duplex_03, _notpowered)
tgv_rd_graphics_template(tgv_rd_05, tgv_duplex_05, )
tgv_rd_graphics_template(tgv_rd_06, tgv_duplex_06, )
tgv_rd_graphics_template(tgv_rd_06, tgv_duplex_06, _notpowered)
tgv_rd_graphics_template(tgv_rd_09, tgv_duplex_09, )
tgv_rd_graphics_template(tgv_rd_09, tgv_duplex_09, _notpowered)

switch (FEAT_TRAINS, SELF, tgv_rd_h_sprites_left_check,
  position_in_consist == 0)
{
  1: tgv_rd_h_sprites_left;
     tgv_rd_h_middle_sprites_left;
}

switch (FEAT_TRAINS, SELF, tgv_rd_h_sprites_right_check,
  position_in_consist_from_end == 1)
{
  1: tgv_rd_h_sprites_right;
     tgv_rd_h_middle_sprites_right;
}

switch (FEAT_TRAINS, SELF, tgv_rd_sprites_start,
[  set_offset_to(-position_in_articulated_veh),
   (no_tgv_inR4() * 200) +
   (prev_vehicle_randombits() & 0x01) * 100 +
   position_in_articulated_veh])
{
   0: tgv_rd_h_sprites_left_check;
   2: tgv_rd_02_sprites_left;
   4: tgv_rd_03_sprites_left;
   6: tgv_rd_03_sprites_left;
   8: tgv_rd_05_sprites_left;
  10: tgv_rd_06_sprites_left;
  12: tgv_rd_06_sprites_right;
  14: tgv_rd_06_sprites_right;
  16: tgv_rd_09_sprites_right;
  18: tgv_rd_h_sprites_right_check;

 100: tgv_rd_h_sprites_left_check;
 102: tgv_rd_09_sprites_left;
 104: tgv_rd_06_sprites_left;
 106: tgv_rd_06_sprites_left;
 108: tgv_rd_06_sprites_right;
 110: tgv_rd_05_sprites_right;
 112: tgv_rd_03_sprites_right;
 114: tgv_rd_03_sprites_right;
 116: tgv_rd_02_sprites_right;
 118: tgv_rd_h_sprites_right_check;

 200: tgv_rd_h_notpowered_sprites_left;
 202: tgv_rd_02_notpowered_sprites_left;
 204: tgv_rd_03_notpowered_sprites_left;
 206: tgv_rd_03_notpowered_sprites_left;
 208: tgv_rd_05_sprites_left;
 210: tgv_rd_06_notpowered_sprites_left;
 212: tgv_rd_06_notpowered_sprites_right;
 214: tgv_rd_06_notpowered_sprites_right;
 216: tgv_rd_09_notpowered_sprites_right;
 218: tgv_rd_h_notpowered_sprites_right;

 300: tgv_rd_h_notpowered_sprites_left;
 302: tgv_rd_09_notpowered_sprites_left;
 304: tgv_rd_06_notpowered_sprites_left;
 306: tgv_rd_06_notpowered_sprites_left;
 308: tgv_rd_06_notpowered_sprites_right;
 310: tgv_rd_05_sprites_right;
 312: tgv_rd_03_notpowered_sprites_right;
 314: tgv_rd_03_notpowered_sprites_right;
 316: tgv_rd_02_notpowered_sprites_right;
 318: tgv_rd_h_notpowered_sprites_right;

      align_11_sprites;
}

long_vehicle(tgv_rd)

switch (FEAT_TRAINS, SELF, tgv_rd_articulated_part,
  extra_callback_info1)
{
  1: return dummy4;
  2: return tgv_rd_part2;
  3: return dummy4;
  4: return tgv_rd_part3;
  5: return dummy3;
  6: return tgv_rd_part3;
  7: return dummy3;
  8: return tgv_rd_part3;
  9: return dummy3;
 10: return tgv_rd_part3;
 11: return dummy3;
 12: return tgv_rd_part3;
 13: return dummy3;
 14: return tgv_rd_part3;
 15: return dummy3;
 16: return tgv_rd_part3;
 17: return dummy4;
 18: return tgv_rd_part3;
 19: return dummy4;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, tgv_rd_create_effect_01_d,
  electric_create_effect(3)) { return 1; }

is_ER15DC_ds(tgv_rd_create_effect_01_2, tgv_rd_create_effect_01_d, return disable_create_effect())
is_ER3DC_ds(tgv_rd_create_effect_01, tgv_rd_create_effect_01_d, tgv_rd_create_effect_01_2)

switch (FEAT_TRAINS, SELF, tgv_rd_create_effect_10,
  electric_create_effect(-2)) { return 1; }

switch (FEAT_TRAINS, SELF, tgv_rd_create_effect0,
  position_in_articulated_veh)
{
   0: tgv_rd_create_effect_01;
  18: tgv_rd_create_effect_10;
      return disable_create_effect();
}

switch (FEAT_TRAINS, PARENT, tgv_rd_create_effect,
  any_tgv())
{
  1: tgv_rd_create_effect0;
     return disable_create_effect();
}

switch (FEAT_TRAINS, PARENT, tgv_rd_can_attach_wagon1,
  tgvs_count() < 2)
{
  1: return CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, SELF, tgv_rd_can_attach_wagon,
  any_tgv())
{
  1: tgv_rd_can_attach_wagon1;
     return string(STR_CAN_ATTACH_TGV);
}

switch (FEAT_TRAINS, SELF, tgv_rd_cargo_subtype_text,
  cargo_subtype)
{
  LV_SNCF_CARMILLON: return string(STR_REFIT_LV_SNCF_CARMILLON);
                     return string(STR_REFIT_LV_SNCF_ATLANTIQUE);
}

RC_head_check_tgv(tgv_rd)
switch (FEAT_TRAINS, SELF, tgv_rd_running_cost_factor_AC,
[  STORE_TEMP(1561, 0),  // Моторы
   STORE_TEMP(219, 1),   // Бригада
   STORE_TEMP(548, 2),   // Износ
   STORE_TEMP(0, 3),     // Сопровождение
   STORE_TEMP(383, 4),   // ТО
   STORE_TEMP(27, 5),    // Сертификация

   STORE_TEMP(320, 6),                                 // Скорость
   STORE_TEMP(383, 7),                                 // Тара
   STORE_TEMP(LOAD_TEMP(7) + 348 / 16 + 197 / 8, 8)])  // Максимальная масса
{ tgv_rd_check_running_cost_factor; }
switch (FEAT_TRAINS, SELF, tgv_rd_running_cost_factor_DC,
[  STORE_TEMP(1561, 0),  // Моторы
   STORE_TEMP(219, 1),   // Бригада
   STORE_TEMP(548, 2),   // Износ
   STORE_TEMP(0, 3),     // Сопровождение
   STORE_TEMP(383, 4),   // ТО
   STORE_TEMP(27, 5),    // Сертификация

   STORE_TEMP(250, 6),                                 // Скорость
   STORE_TEMP(383, 7),                                 // Тара
   STORE_TEMP(LOAD_TEMP(7) + 348 / 16 + 197 / 8, 8)])  // Максимальная масса
{ tgv_rd_check_running_cost_factor; }

switch (FEAT_TRAINS, SELF, tgv_rd_running_cost_factor, is_ER25AC() || is_ER15AC())
{ 1: tgv_rd_running_cost_factor_AC;
     tgv_rd_running_cost_factor_DC; }

engine_capacity_MU_tgv(tgv_rd, 348)
engine_power_MU_tgv(tgv_rd_15DC, kw2hp(3680))
engine_power_MU_tgv(tgv_rd_DC, kw2hp(3680))
engine_power_MU_tgv(tgv_rd_AC, kw2hp(8800))
engine_power_change_AC_DDC(tgv_rd)
engine_speed(tgv_rd_DC, 250)
engine_speed(tgv_rd_AC, 320)
engine_speed_change_AC_DC(tgv_rd)
hint_MU_comment_chande_speedpower_on_dc(tgv_rd,
     calc_loading(max(348 / (4 * WAGON_DOOR), 197 / (3 * WAGON_DOOR))),
     STR_PURCHASE_HINT_CAP_001_NORMAL,
     STR_PURCHASE_HINT_ENGINE_TYPE_EMU_T_ACDDC,
     STR_SECTIONS_MU_S1_2of10,
     1995, 1998,
     string(STR_PURCHASE_HINT_FACTORY_ALSTOM_TRANS),
     string(STR_PURCHASE_HINT_TGV),
     250, kw2hp_in_cb(3680))

item (FEAT_TRAINS, tgv_rd, 665) {
  property {
    name: string(STR_NAME_TGV_RD);
    vehicle_dates(1995, 1998, 30, 10, 8, 1500)
    vehicle_emu(acddc, kw2hp(8800), 383, 400.0, 348, 4 * WAGON_DOOR, _highspeed)
  }
  graphics {
        additional_text: tgv_rd_additional_text;
       articulated_part: tgv_rd_articulated_part;
       can_attach_wagon: tgv_rd_can_attach_wagon;
       cargo_age_period: return CAP_001_NORMAL;
         cargo_capacity: tgv_rd_cargo_capacity;
     cargo_subtype_text: tgv_rd_cargo_subtype_text;
         colour_mapping: any_cc_colour;
            cost_factor: return 1500;
          create_effect: tgv_rd_create_effect;
                default: tgv_rd_sprites;
                  power: tgv_rd_power;
               purchase: tgv_rd_purchase_sprites;
         purchase_power: return get_power_in_cb(11965);
         purchase_speed: return 320;
    running_cost_factor: tgv_rd_running_cost_factor;
                  speed: tgv_rd_speed;
  }
}

long_name_template(tgv_rd, STR_LONGNAME_TGV_RD)

engine_capacity_MU_tgv(tgv_rd_part2, 197)

item (FEAT_TRAINS, tgv_rd_part2, 667) {
  property {
    vehicle_no_dates(1995, 1998, 30, 10, 8, 0)
    vehicle_emu_c(acddc, 0, 197, 3 * WAGON_DOOR, _highspeed)
    vehicle_tourists()
    default_cargo_type: TOUR;
  }
  graphics {
    cargo_age_period: return CAP_001_NORMAL;
      cargo_capacity: tgv_rd_part2_cargo_capacity;
      colour_mapping: any_cc_colour;
       create_effect: tgv_rd_create_effect;
             default: tgv_rd_sprites;
          refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}

item (FEAT_TRAINS, tgv_rd_part3, 668) {
  property {
    vehicle_no_dates(1995, 1998, 30, 10, 8, 0)
    vehicle_emu_empty(acddc, _highspeed)
  }
  graphics {
    colour_mapping: any_cc_colour;
     create_effect: tgv_rd_create_effect;
           default: tgv_rd_sprites;
  }
}
