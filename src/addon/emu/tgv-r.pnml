// tgv_r

#define IMAGEFILE  "src/addon/emu/tgv-r_01.png"
purchase_sprites_with_icon2(tgv_r, 18, 0, 25ac_ddc, none)
MU_head_2d_middle_sp_sprites(12, tgv_r_h_v1, 32, 40)
MU_head_2d_middle_sp_sprites(12, tgv_r_h_v2, 32, 576)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-r-pba_01.png"
MU_head_2d_middle_sp_sprites(12, tgv_r_h_v3, 32, 40)
MU_head_2d_middle_sp_sprites(12, tgv_r_h_v4, 32, 576)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-r_02.png"
MU_car_sprites(12, tgv_r_02_v1, 32, 40)
MU_car_sprites(12, tgv_r_02_v2, 32, 308)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-r-pba_02.png"
MU_car_sprites(12, tgv_r_02_v3, 32, 40)
MU_car_sprites(12, tgv_r_02_v4, 32, 308)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-r_03.png"
MU_car_sprites(11, tgv_r_03_v1, 32, 40)
MU_car_sprites(11, tgv_r_03_v2, 32, 308)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-r-pba_03.png"
MU_car_sprites(11, tgv_r_03_v3, 32, 40)
MU_car_sprites(11, tgv_r_03_v4, 32, 308)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-r-pba_04.png"
MU_car_sprites(11, tgv_r_04_v3, 32, 40)
MU_car_sprites(11, tgv_r_04_v4, 32, 308)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-r_05.png"
MU_car_nd_sprites(11, tgv_r_05_v1, 32, 40)
MU_car_nd_sprites(11, tgv_r_05_v2, 32, 308)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-r-pba_05.png"
MU_car_nd_sprites(11, tgv_r_05_v3, 32, 40)
MU_car_nd_sprites(11, tgv_r_05_v4, 32, 308)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-r_06.png"
MU_car_sprites(11, tgv_r_06_v1, 32, 40)
MU_car_sprites(11, tgv_r_06_v2, 32, 308)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-r-pba_06.png"
MU_car_sprites(11, tgv_r_06_v3, 32, 40)
MU_car_sprites(11, tgv_r_06_v4, 32, 308)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-r-pba_07.png"
MU_car_sprites(11, tgv_r_07_v3, 32, 40)
MU_car_sprites(11, tgv_r_07_v4, 32, 308)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-r_09.png"
MU_car_sprites(12, tgv_r_09_v1, 32, 40)
MU_car_sprites(12, tgv_r_09_v2, 32, 308)
#undef IMAGEFILE
#define IMAGEFILE  "src/addon/emu/tgv-r-pba_09.png"
MU_car_sprites(12, tgv_r_09_v3, 32, 40)
MU_car_sprites(12, tgv_r_09_v4, 32, 308)
#undef IMAGEFILE

#define tgv_r_graphics_template(name, name2, suffix)                \
switch (FEAT_TRAINS, SELF, name##suffix##_sprites_left_pba_check,   \
  check_year(2009))                                                 \
{ 0: name##_v3##suffix##_sprites_left;                              \
     name##_v4##suffix##_sprites_left; }                            \
switch (FEAT_TRAINS, SELF, name##suffix##_sprites_left,             \
[ set_offset_to(-position_in_articulated_veh),                      \
  prev_vehicle_cargo_subtype()])                                    \
{                  0: name2##_v1##suffix##_sprites_left;            \
  LV_SNCF_ATLANTIQUE: name2##_v1##suffix##_sprites_left;            \
   LV_SNCF_CARMILLON: name2##_v2##suffix##_sprites_left;            \
           LV_THALYS: name##suffix##_sprites_left_pba_check;        \
     align_12_sprites; }                                            \
switch (FEAT_TRAINS, SELF, name##suffix##_sprites_right_pba_check,  \
  check_year(2009))                                                 \
{ 0: name##_v3##suffix##_sprites_right;                             \
     name##_v4##suffix##_sprites_right; }                           \
switch (FEAT_TRAINS, SELF, name##suffix##_sprites_right,            \
[ set_offset_to(-position_in_articulated_veh),                      \
  prev_vehicle_cargo_subtype()])                                    \
{                  0: name2##_v1##suffix##_sprites_right;           \
  LV_SNCF_ATLANTIQUE: name2##_v1##suffix##_sprites_right;           \
   LV_SNCF_CARMILLON: name2##_v2##suffix##_sprites_right;           \
           LV_THALYS: name##suffix##_sprites_right_pba_check;       \
     align_12_sprites; }                                            \

tgv_r_graphics_template(tgv_r_h,  tgv_r_h,  )
tgv_r_graphics_template(tgv_r_h,  tgv_r_h,  _middle)
tgv_r_graphics_template(tgv_r_h,  tgv_r_h,  _notpowered)
tgv_r_graphics_template(tgv_r_02, tgv_r_02, )
tgv_r_graphics_template(tgv_r_02, tgv_r_02, _notpowered)
tgv_r_graphics_template(tgv_r_03, tgv_r_03, )
tgv_r_graphics_template(tgv_r_03, tgv_r_03, _notpowered)
tgv_r_graphics_template(tgv_r_04, tgv_r_03, )
tgv_r_graphics_template(tgv_r_04, tgv_r_03, _notpowered)
tgv_r_graphics_template(tgv_r_05, tgv_r_05, )
tgv_r_graphics_template(tgv_r_06, tgv_r_06, )
tgv_r_graphics_template(tgv_r_06, tgv_r_06, _notpowered)
tgv_r_graphics_template(tgv_r_07, tgv_r_06, )
tgv_r_graphics_template(tgv_r_07, tgv_r_06, _notpowered)
tgv_r_graphics_template(tgv_r_09, tgv_r_09, )
tgv_r_graphics_template(tgv_r_09, tgv_r_09, _notpowered)

switch (FEAT_TRAINS, SELF, tgv_r_h_sprites_left_check,
  position_in_consist == 0)
{
  1: tgv_r_h_sprites_left;
     tgv_r_h_middle_sprites_left;
}

switch (FEAT_TRAINS, SELF, tgv_r_h_sprites_right_check,
  position_in_consist_from_end == 1)
{
  1: tgv_r_h_sprites_right;
     tgv_r_h_middle_sprites_right;
}

switch (FEAT_TRAINS, SELF, tgv_r_sprites_start,
[  set_offset_to(-position_in_articulated_veh),
   (no_tgv_inR4() * 200) +
   (prev_vehicle_randombits() & 0x01) * 100 +
   position_in_articulated_veh])
{
   0: tgv_r_h_sprites_left_check;
   2: tgv_r_02_sprites_left;
   4: tgv_r_03_sprites_left;
   6: tgv_r_04_sprites_left;
   8: tgv_r_05_sprites_left;
  10: tgv_r_06_sprites_left;
  12: tgv_r_07_sprites_right;
  14: tgv_r_06_sprites_right;
  16: tgv_r_09_sprites_right;
  18: tgv_r_h_sprites_right_check;

 100: tgv_r_h_sprites_left_check;
 102: tgv_r_09_sprites_left;
 104: tgv_r_06_sprites_left;
 106: tgv_r_07_sprites_left;
 108: tgv_r_06_sprites_right;
 110: tgv_r_05_sprites_right;
 112: tgv_r_04_sprites_right;
 114: tgv_r_03_sprites_right;
 116: tgv_r_02_sprites_right;
 118: tgv_r_h_sprites_right_check;

 200: tgv_r_h_notpowered_sprites_left;
 202: tgv_r_02_notpowered_sprites_left;
 204: tgv_r_03_notpowered_sprites_left;
 206: tgv_r_04_notpowered_sprites_left;
 208: tgv_r_05_sprites_left;
 210: tgv_r_06_notpowered_sprites_left;
 212: tgv_r_07_notpowered_sprites_right;
 214: tgv_r_06_notpowered_sprites_right;
 216: tgv_r_09_notpowered_sprites_right;
 218: tgv_r_h_notpowered_sprites_right;

 300: tgv_r_h_notpowered_sprites_left;
 302: tgv_r_09_notpowered_sprites_left;
 304: tgv_r_06_notpowered_sprites_left;
 306: tgv_r_07_notpowered_sprites_left;
 308: tgv_r_06_notpowered_sprites_right;
 310: tgv_r_05_sprites_right;
 312: tgv_r_04_notpowered_sprites_right;
 314: tgv_r_03_notpowered_sprites_right;
 316: tgv_r_02_notpowered_sprites_right;
 318: tgv_r_h_notpowered_sprites_right;

      align_11_sprites;
}

long_vehicle(tgv_r)

switch (FEAT_TRAINS, SELF, tgv_r_articulated_part,
  extra_callback_info1)
{
  1: return dummy4;
  2: return tgv_r_part2;
  3: return dummy4;
  4: return tgv_r_part3;
  5: return dummy3;
  6: return tgv_r_part3;
  7: return dummy3;
  8: return tgv_r_part3;
  9: return dummy3;
 10: return tgv_r_part3;
 11: return dummy3;
 12: return tgv_r_part3;
 13: return dummy3;
 14: return tgv_r_part3;
 15: return dummy3;
 16: return tgv_r_part3;
 17: return dummy4;
 18: return tgv_r_part3;
 19: return dummy4;
     return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, tgv_r_create_effect_01_d, electric_create_effect(3)) { return 1; }
switch (FEAT_TRAINS, SELF, tgv_r_create_effect_10, electric_create_effect(-2)) { return 1; }

is_ER15DC_ds(tgv_r_create_effect_01_2, tgv_r_create_effect_01_d, return disable_create_effect())
is_ER3DC_ds(tgv_r_create_effect_01, tgv_r_create_effect_01_d, tgv_r_create_effect_01_2)

switch (FEAT_TRAINS, SELF, tgv_r_create_effect0,
  position_in_articulated_veh)
{
   0: tgv_r_create_effect_01;
  18: tgv_r_create_effect_10;
      return disable_create_effect();
}

switch (FEAT_TRAINS, PARENT, tgv_r_create_effect,
  any_tgv())
{
  1: tgv_r_create_effect0;
     return disable_create_effect();
}

switch (FEAT_TRAINS, PARENT, tgv_r_can_attach_wagon1,
  tgvs_count() < 2)
{
  1: return CB_RESULT_ATTACH_ALLOW;
     return string(STR_CAN_ATTACH_NO_MORE);
}

switch (FEAT_TRAINS, SELF, tgv_r_can_attach_wagon,
  any_tgv())
{
  1: tgv_r_can_attach_wagon1;
     return string(STR_CAN_ATTACH_TGV);
}

switch (FEAT_TRAINS, SELF, tgv_r_cargo_subtype_text,
  cargo_subtype)
{
  LV_SNCF_CARMILLON: return string(STR_REFIT_LV_SNCF_CARMILLON);
          LV_THALYS: return string(STR_REFIT_LV_THALYS);
                     return string(STR_REFIT_LV_SNCF_ATLANTIQUE);
}

RC_head_check_tgv(tgv_r)
switch (FEAT_TRAINS, SELF, tgv_r_running_cost_factor_AC,
[  STORE_TEMP(1561, 0),  // Моторы
   STORE_TEMP(219, 1),   // Бригада
   STORE_TEMP(548, 2),   // Износ
   STORE_TEMP(0, 3),     // Сопровождение
   STORE_TEMP(383, 4),   // ТО
   STORE_TEMP(27, 5),    // Сертификация

   STORE_TEMP(320, 6),                                 // Скорость
   STORE_TEMP(383, 7),                                 // Тара
   STORE_TEMP(LOAD_TEMP(7) + 257 / 16 + 120 / 8, 8)])  // Максимальная масса
{ tgv_r_check_running_cost_factor; }
switch (FEAT_TRAINS, SELF, tgv_r_running_cost_factor_DC,
[  STORE_TEMP(1561, 0),  // Моторы
   STORE_TEMP(219, 1),   // Бригада
   STORE_TEMP(548, 2),   // Износ
   STORE_TEMP(0, 3),     // Сопровождение
   STORE_TEMP(383, 4),   // ТО
   STORE_TEMP(27, 5),    // Сертификация

   STORE_TEMP(250, 6),                                 // Скорость
   STORE_TEMP(383, 7),                                 // Тара
   STORE_TEMP(LOAD_TEMP(7) + 257 / 16 + 120 / 8, 8)])  // Максимальная масса
{ tgv_r_check_running_cost_factor; }

switch (FEAT_TRAINS, SELF, tgv_r_running_cost_factor, is_ER25AC() || is_ER15AC())
{ 1: tgv_r_running_cost_factor_AC;
     tgv_r_running_cost_factor_DC; }

engine_capacity_MU_tgv(tgv_r, 257)
engine_power_MU_tgv(tgv_r_15DC, kw2hp(3680))
engine_power_MU_tgv(tgv_r_DC, kw2hp(3680))
engine_power_MU_tgv(tgv_r_AC, kw2hp(8800))
engine_power_change_AC_DDC(tgv_r)
engine_speed(tgv_r_DC, 250)
engine_speed(tgv_r_AC, 320)
engine_speed_change_AC_DC(tgv_r)
hint_MU_comment_chande_speedpower_on_dc(tgv_r,
     calc_loading(max(257 / (4 * WAGON_DOOR), 120 / (3 * WAGON_DOOR))),
     STR_PURCHASE_HINT_CAP_001_NORMAL,
     STR_PURCHASE_HINT_ENGINE_TYPE_EMU_T_ACDDC,
     STR_SECTIONS_MU_S1_2of10,
     1992, 1996,
     string(STR_PURCHASE_HINT_FACTORY_ALSTOM_TRANS),
     string(STR_PURCHASE_HINT_TGV),
     250, kw2hp_in_cb(3680))

item (FEAT_TRAINS, tgv_r, 134) {
  property {
    name: string(STR_NAME_TGV_R);
    vehicle_dates(1992, 1996, 30, 10, 8, 1500)
    vehicle_emu(acddc, kw2hp(8800), 383, 400.0, 257, 4 * WAGON_DOOR, _highspeed)
  }
  graphics {
        additional_text: tgv_r_additional_text;
       articulated_part: tgv_r_articulated_part;
       can_attach_wagon: tgv_r_can_attach_wagon;
       cargo_age_period: return CAP_001_NORMAL;
         cargo_capacity: tgv_r_cargo_capacity;
     cargo_subtype_text: tgv_r_cargo_subtype_text;
         colour_mapping: any_cc_colour;
            cost_factor: return 1500;
          create_effect: tgv_r_create_effect;
                default: tgv_r_sprites;
                  power: tgv_r_power;
               purchase: tgv_r_purchase_sprites;
         purchase_power: return get_power_in_cb(kw2hp(8800));
         purchase_speed: return 320;
    running_cost_factor: tgv_r_running_cost_factor;
                  speed: tgv_r_speed;
  }
}

long_name_template(tgv_r, STR_LONGNAME_TGV_R)

engine_capacity_MU_tgv(tgv_r_part2, 120)

item (FEAT_TRAINS, tgv_r_part2, 124) {
  property {
    vehicle_no_dates(1992, 1996, 30, 10, 8, 0)
    vehicle_emu_c(acddc, 0, 120, 3 * WAGON_DOOR, _highspeed)
    vehicle_tourists()
    default_cargo_type: TOUR;
  }
  graphics {
    cargo_age_period: return CAP_001_NORMAL;
      cargo_capacity: tgv_r_part2_cargo_capacity;
      colour_mapping: any_cc_colour;
       create_effect: tgv_r_create_effect;
             default: tgv_r_sprites;
          refit_cost: return 0 | CB_RESULT_AUTOREFIT;
  }
}

item (FEAT_TRAINS, tgv_r_part3, 125) {
  property {
    vehicle_no_dates(1992, 1996, 30, 10, 8, 0)
    vehicle_emu_empty(acddc, _highspeed)
  }
  graphics {
    colour_mapping: any_cc_colour;
     create_effect: tgv_r_create_effect;
           default: tgv_r_sprites;
  }
}
