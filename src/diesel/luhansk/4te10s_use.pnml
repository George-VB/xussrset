// 4te10s usex 2te10s

#define IMAGEFILE  "src/diesel/luhansk/4te10s.png"
purchase_sprites_with_icon(_4te10s, 018, 19, diesel)
#undef IMAGEFILE

// Если хотя бы 4 не следуют др. за др., то не выпускать
switch (FEAT_TRAINS, PARENT, _4te10s_start_stop5,
[
  set_offset_to(2),
  STORE_TEMP((prev_vehicle_type_id() == _4te10s) || (prev_vehicle_type_id() == _4te10s_m), 0),
  set_offset_to(4),
  STORE_TEMP(((prev_vehicle_type_id() == _4te10s) || (prev_vehicle_type_id() == _4te10s_m)) && LOAD_TEMP(0), 0),
  set_offset_to(6),
  ((prev_vehicle_type_id() == _4te10s) || (prev_vehicle_type_id() == _4te10s_m)) && LOAD_TEMP(0)
])
{
  1: any_start_stop;
  return string(STR_START_STOP_NEED_AT_LEAST_4_CONSECUTIVE_ENGINE);
}

// Если локомотивов меньше 4, то не выпускать
switch (FEAT_TRAINS, PARENT, _4te10s_start_stop_b1993,
  (count_veh_id(_4te10s) >= 2) && (count_veh_id(_4te10s_m) >= 2))
{
  1: _4te10s_start_stop5;
  return string(STR_START_STOP_NEED_2AND2_OR_MORE_ENGINE);
}

// Если хотя бы 3 не следуют др. за др., то не выпускать
switch (FEAT_TRAINS, PARENT, _4te10s_start_stop3,
[
  set_offset_to(2),
  STORE_TEMP((prev_vehicle_type_id() == _4te10s) || (prev_vehicle_type_id() == _4te10s_m), 0),
  set_offset_to(4),
  ((prev_vehicle_type_id() == _4te10s) || (prev_vehicle_type_id() == _4te10s_m)) && LOAD_TEMP(0)
])
{
  1: any_start_stop;
  return string(STR_START_STOP_NEED_AT_LEAST_3_CONSECUTIVE_ENGINE);
}

// Если локомотивов меньше 3, то не выпускать
switch (FEAT_TRAINS, PARENT, _4te10s_start_stop_a1993,
  (count_veh_id(_4te10s) >= 2) && (count_veh_id(_4te10s_m) >= 1))
{
  1: _4te10s_start_stop3;
  return string(STR_START_STOP_NEED_2AND1_OR_MORE_ENGINE);
}

// Если год >= 1993, можно в 3 секции
switch (FEAT_TRAINS, PARENT, _4te10s_start_stop,
  current_year >= 1993)
{
  1: _4te10s_start_stop_a1993;
     _4te10s_start_stop_b1993;
}

engine_speed(_4te10s, 100)
switch (FEAT_TRAINS, SELF, _4te10s_running_cost_factor,
[  STORE_TEMP(272, 0),  // Моторы
   STORE_TEMP(38, 1),   // Бригада
   STORE_TEMP(96, 2),   // Износ
   STORE_TEMP(0, 3),    // Сопровождение
   STORE_TEMP(67, 4),   // ТО
   STORE_TEMP(5, 5),    // Сертификация

   STORE_TEMP(100, 6),            // Скорость
   STORE_TEMP(138, 7),            // Тара
   STORE_TEMP(LOAD_TEMP(7), 8)])  // Максимальная масса
{ all_running_cost_factor; }      // 478

name_in_group_varsub_subgroup(_4te10s, string(STR_NAME_IN_GROUP, string(STR_NAME_TE10_SERIES), string(STR_NAME_4TE10S)),
                                       string(STR_NAME_IN_GROUP, string(STR_NAME_TE10_SERIES), string(STR_LONGNAME_4TE10S)),
                                       fact_luhansk(), string(STR_NAME_4TE10S))

hint_engine_with_sme(_4te10s,
     STR_PURCHASE_HINT_ENGINE_TYPE_FREIGHT_DIESEL,
     STR_PURCHASE_HINT_SECTIONS_3_4M_WITH_ATTACH, STR_NAME_2TE10S,
     1983, 1987,
     fact_luhansk())

item (FEAT_TRAINS, _4te10s, 350) {
  property {
    name: string(STR_NAME_4TE10S);
    vehicle_dates(1983, 1987, 30, 10, 8, 49)
    vehicle_cargo()
    vehicle_diesel(2273, 138, 398.6, )
    vehicle_group(subgroup_te10_lz)
  }
  graphics {
         additional_text: _4te10s_additional_text;
        articulated_part: articulated_part_dummy2;
    bitmask_vehicle_info: _2te10s_bitmask_vehicle_info;
        can_attach_wagon: any_can_attach_wagon;
          cargo_capacity: return 0;
          colour_mapping: any_cc_colour;
           create_effect: _2te10s_create_effect_sprites_start;
                 default: _2te10s_sprites;
                    name: _4te10s_name;
                purchase: _4te10s_purchase_sprites;
          purchase_speed: return 100;
     running_cost_factor: _4te10s_running_cost_factor;
                   speed: _4te10s_speed;
              start_stop: _4te10s_start_stop;
  }
}

long_name_template(_4te10s, STR_LONGNAME_4TE10S)
models_default_cargo_template(_4te10s)
allow_diesel(_4te10s)
